#!/bin/csh -fx
# Render images for a PCB
setenv ARGS "--zoom=0.4 --floor --width=5000 --height=5000 --quality=high --preset=follow_pcb_editor"
#setenv ARGS "$ARGS --background=opaque"
setenv BASE "$1:r"
setenv BOARD "$BASE:t"
setenv TMP `mktemp -u`.kicad_pcb
setenv PNG `mktemp -u`.png
setenv QR "PCB-$BOARD-`date +%Y-%m-%d`"

# Main view
$0:h/clean -i "$BASE.kicad_pcb" -o "$TMP" --qr="$QR"
/Applications/KiCad/KiCad.app/Contents/MacOS/kicad-cli pcb render -o "$PNG" "$TMP" --perspective --rotate ' -40,0,30' $ARGS
/Applications/GIMP-2.10.app/Contents/MacOS/gimp -i -b '(crop-png "'"$PNG"'")' -b '(gimp-quit 0)'

# See if there is a panel (User.2)
$0:h/clean -i "$BASE.kicad_pcb" -o "$TMP" --case=2 --qr="$QR"
if(! $status) then
	# There is a panel, so this is panel, and we need to render cut out as main
	setenv panel
	mv -f "$PNG" "$BASE-panel.png"
	/Applications/KiCad/KiCad.app/Contents/MacOS/kicad-cli pcb render -o "$PNG" "$TMP" --perspective --rotate ' -40,0,30' $ARGS
	/Applications/GIMP-2.10.app/Contents/MacOS/gimp -i -b '(crop-png "'"$PNG"'")' -b '(gimp-quit 0)'
endif
mv -f "$PNG" "$BASE.png"

# 90 degree rotate top view
/Applications/KiCad/KiCad.app/Contents/MacOS/kicad-cli pcb render -o "$PNG" "$TMP" --perspective --rotate ' -40,0,-60' $ARGS
/Applications/GIMP-2.10.app/Contents/MacOS/gimp -i -b '(crop-png "'"$PNG"'")' -b '(gimp-quit 0)'
mv -f "$PNG" "$BASE-90.png"

# Bottom view
$0:h/clean -i "$BASE.kicad_pcb" -o "$TMP" --case=2 --qr="$QR"
/Applications/KiCad/KiCad.app/Contents/MacOS/kicad-cli pcb render -o "$PNG" "$TMP" --perspective --rotate ' -40,180,-30' $ARGS
/Applications/GIMP-2.10.app/Contents/MacOS/gimp -i -b '(crop-png "'"$PNG"'")' -b '(gimp-quit 0)'
mv -f "$PNG" "$BASE-bottom.png"

if($?panel) then
	$0:h/clean -i "$BASE.kicad_pcb" -o "$TMP" --qr="$QR" --eco-1
	/Applications/KiCad/KiCad.app/Contents/MacOS/kicad-cli pcb render -o "$PNG" "$TMP" --perspective --rotate ' -40,180,-30' $ARGS
	/Applications/GIMP-2.10.app/Contents/MacOS/gimp -i -b '(crop-png "'"$PNG"'")' -b '(gimp-quit 0)'
	mv -f "$PNG" "$BASE-panel-bottom.png"
endif

# Alt cut out view
$0:h/clean -i "$BASE.kicad_pcb" -o "$TMP" --case=4 --qr="$QR"
if(! $status) then
	/Applications/KiCad/KiCad.app/Contents/MacOS/kicad-cli pcb render -o "$PNG" "$TMP" --perspective --rotate ' -40,0,30' $ARGS
	/Applications/GIMP-2.10.app/Contents/MacOS/gimp -i -b '(crop-png "'"$PNG"'")' -b '(gimp-quit 0)'
	mv -f "$PNG" "$BASE-alt.png"
	/Applications/KiCad/KiCad.app/Contents/MacOS/kicad-cli pcb render -o "$PNG" "$TMP" --perspective --rotate ' -40,0,-60' $ARGS
	/Applications/GIMP-2.10.app/Contents/MacOS/gimp -i -b '(crop-png "'"$PNG"'")' -b '(gimp-quit 0)'
	mv -f "$PNG" "$BASE-alt-90.png"
	$0:h/clean -i "$BASE.kicad_pcb" -o "$TMP" --case=4 --qr="$QR" --eco-1
	/Applications/KiCad/KiCad.app/Contents/MacOS/kicad-cli pcb render -o "$PNG" "$TMP" --perspective --rotate ' -40,180,-30' $ARGS
	/Applications/GIMP-2.10.app/Contents/MacOS/gimp -i -b '(crop-png "'"$PNG"'")' -b '(gimp-quit 0)'
	mv -f "$PNG" "$BASE-alt-bottom.png"
endif

# README is missing
setenv RM "$BASE:h/README.md"
if(! -e "$RM") then
	echo "# $BOARD" > "$RM"
	echo "" >> "$RM"
	echo "\![Top]($BOARD.png)" >> "$RM"
	echo "\![Top rotated]($BOARD-90.png)" >> "$RM"
	echo "\![Bottom]($BOARD-bottom.png)" >> "$RM"
	if(-e "$BASE-panel.png") then
		echo "\![Panel]($BASE:t-panel.png)" >> "$RM"
	endif
	if(-e "$BASE-alt.png") then
		echo "\![Panel]($BASE:t-alt.png)" >> "$RM"
	endif
endif

# Done
rm -f "$TMP"
