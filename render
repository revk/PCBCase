#!/bin/csh -f
# Render images for a PCB
setenv ARGS "--zoom=0.4 --floor --width=5000 --height=5000 --quality=high --preset=follow_pcb_editor"
#setenv ARGS "$ARGS --background=opaque"
setenv BASE "$1:r"
setenv BOARD "$BASE:t"
setenv TMP `mktemp -u`.kicad_pcb
setenv PNG `mktemp -u`.png
setenv QR "PCB-$BOARD-`date +%Y-%m-%d`"
setenv BARCODE "--dm --qr='$QR'"

# Note scm files to /Users/adrian//Library/Application Support/GIMP/2.10/scripts/

# Main view
$0:h/clean -i "$BASE.kicad_pcb" -o "$TMP" $BARCODE
/Applications/KiCad/KiCad.app/Contents/MacOS/kicad-cli pcb render -o "$PNG" "$TMP" --perspective --rotate ' -40,0,30' $ARGS
/Applications/GIMP-2.10.app/Contents/MacOS/gimp -i -b '(crop-png "'"$PNG"'")' -b '(gimp-quit 0)'

# See if there is a panel (User.2)
$0:h/clean -i "$BASE.kicad_pcb" -o "$TMP" --case=2 $BARCODE
if(! $status) then
	# There is a panel, so this is panel, and we need to render cut out as main
	setenv panel
	mv -f "$PNG" "$BASE-panel.png"
	/Applications/KiCad/KiCad.app/Contents/MacOS/kicad-cli pcb render -o "$PNG" "$TMP" --perspective --rotate ' -40,0,30' $ARGS
	/Applications/GIMP-2.10.app/Contents/MacOS/gimp -i -b '(crop-png "'"$PNG"'")' -b '(gimp-quit 0)'
endif
mv -f "$PNG" "$BASE.png"

# 90 degree rotate top view
/Applications/KiCad/KiCad.app/Contents/MacOS/kicad-cli pcb render -o "$PNG" "$TMP" --perspective --rotate ' -40,0,-60' $ARGS
/Applications/GIMP-2.10.app/Contents/MacOS/gimp -i -b '(crop-png "'"$PNG"'")' -b '(gimp-quit 0)'
mv -f "$PNG" "$BASE-90.png"

# Bottom view
$0:h/clean -i "$BASE.kicad_pcb" -o "$TMP" --case=2 $BARCODE
/Applications/KiCad/KiCad.app/Contents/MacOS/kicad-cli pcb render -o "$PNG" "$TMP" --perspective --rotate ' -40,180,-30' $ARGS
/Applications/GIMP-2.10.app/Contents/MacOS/gimp -i -b '(crop-png "'"$PNG"'")' -b '(gimp-quit 0)'
mv -f "$PNG" "$BASE-bottom.png"

if($?panel) then
	$0:h/clean -i "$BASE.kicad_pcb" -o "$TMP" $BARCODE
	/Applications/KiCad/KiCad.app/Contents/MacOS/kicad-cli pcb render -o "$PNG" "$TMP" --perspective --rotate ' -40,180,-30' $ARGS
	/Applications/GIMP-2.10.app/Contents/MacOS/gimp -i -b '(crop-png "'"$PNG"'")' -b '(gimp-quit 0)'
	mv -f "$PNG" "$BASE-panel-bottom.png"
endif

# Alt cut out view
$0:h/clean -i "$BASE.kicad_pcb" -o "$TMP" --case=4 $BARCODE
if(! $status) then
	/Applications/KiCad/KiCad.app/Contents/MacOS/kicad-cli pcb render -o "$PNG" "$TMP" --perspective --rotate ' -40,0,30' $ARGS
	/Applications/GIMP-2.10.app/Contents/MacOS/gimp -i -b '(crop-png "'"$PNG"'")' -b '(gimp-quit 0)'
	mv -f "$PNG" "$BASE-alt.png"
	/Applications/KiCad/KiCad.app/Contents/MacOS/kicad-cli pcb render -o "$PNG" "$TMP" --perspective --rotate ' -40,0,-60' $ARGS
	/Applications/GIMP-2.10.app/Contents/MacOS/gimp -i -b '(crop-png "'"$PNG"'")' -b '(gimp-quit 0)'
	mv -f "$PNG" "$BASE-alt-90.png"
	$0:h/clean -i "$BASE.kicad_pcb" -o "$TMP" --case=4 $BARCODE
	/Applications/KiCad/KiCad.app/Contents/MacOS/kicad-cli pcb render -o "$PNG" "$TMP" --perspective --rotate ' -40,180,-30' $ARGS
	/Applications/GIMP-2.10.app/Contents/MacOS/gimp -i -b '(crop-png "'"$PNG"'")' -b '(gimp-quit 0)'
	mv -f "$PNG" "$BASE-alt-bottom.png"
endif

# README is missing
setenv RM "$BASE:h/README.md"
if(-e "$RM") then
	grep -q "Auto generated" "$RM"
	if(! $status) rm -f "$RM"
endif
if(! -e "$RM") then
	cat << END > "$TMP"
# $BOARD

These files are for use in [KiCad](https://www.kicad.org).

## Trademark

This is an open source project, but bear in mind you cannot sell boards bearing the Andrews & Arnold Ltd name, the A&A logo, the registered trademark AJK logo, or the GS1 allocated EANs assigned to Andrews & Arnold Ltd.

## Images

END
	echo -n "<img src='$BOARD.png' width=32%>" >> "$TMP"
	echo -n "<img src='$BOARD-90.png' width=32%>" >> "$TMP"
	echo "<img src='$BOARD-bottom.png' width=32%>" >> "$TMP"
	if(-e "$BASE-alt.png") then
		echo -n "<img src='$BOARD-alt.png' width=32%>" >> "$TMP"
		echo -n "<img src='$BOARD-alt-90.png' width=32%>" >> "$TMP"
		echo "<img src='$BOARD-alt-bottom.png' width=32%>" >> "$TMP"
		endif
	if(-e "$BASE-panel.png") then
		echo -n "<img src='$BOARD-panel.png' width=49%>" >> "$TMP"
		echo "<img src='$BOARD-panel-bottom.png' width=49%>" >> "$TMP"
	endif
	echo "" >> "$TMP"
	echo "*Auto generated `date +%FT%T`*" >> "$TMP"
	mv -f "$TMP" "$RM"
endif

# Done
rm -f "$TMP"
